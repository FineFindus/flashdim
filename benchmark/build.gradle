import com.android.build.api.dsl.ManagedVirtualDevice

plugins {
    id 'com.android.test'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.cyb3rko.flashdim.benchmark'
    compileSdk 34

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    defaultConfig {
        minSdk 33
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments["androidx.benchmark.suppressErrors"] = "LOW-BATTERY"
    }

    buildTypes {
        create("benchmark") {
            resValue 'string', 'app_name', 'FlashDim'
            //minifyEnabled true
            //shrinkResources true
            proguardFiles 'benchmark-rules.pro'
            signingConfig signingConfigs.debug
            matchingFallbacks = ['release']
        }
    }

    testOptions {
        managedDevices {
            devices {
                pixel3Api34(ManagedVirtualDevice) {
                    device = "Pixel 3"
                    apiLevel = 34
                    systemImageSource = "aosp"
                }
            }
        }
    }

    targetProjectPath = ":app"
    experimentalProperties["android.experimental.self-instrumenting"] = true
}

dependencies {
    implementation 'androidx.test.ext:junit:1.1.5'
    implementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    implementation 'androidx.benchmark:benchmark-macro-junit4:1.2.3'
}

androidComponents {
    beforeVariants(selector().all()) {
        enable = buildType == "benchmark"
    }
}